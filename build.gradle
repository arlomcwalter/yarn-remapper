plugins {
    id "java"
    id "application"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

group = "me.seasnail"
archivesBaseName = "yarn-remapper"
mainClassName = "me.seasnail.yarnremapper.Main"

repositories {
    mavenCentral()
    maven {
        name = "Fabric MC"
        url = "https://maven.fabricmc.net/"
    }
}

dependencies {
    implementation "com.google.code.gson:gson:2.8.7"
    implementation "com.google.guava:guava:30.1.1-jre"
    implementation "org.apache.commons:commons-io:1.3.2"
    implementation "net.fabricmc:tiny-remapper:0.5.0"

    // LWJGL - Tiny File Dialogs
    implementation platform("org.lwjgl:lwjgl-bom:3.2.3")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-tinyfd"

    ["natives-windows", "natives-linux", "natives-macos"].forEach {
        runtimeOnly "org.lwjgl:lwjgl::$it"
        runtimeOnly "org.lwjgl:lwjgl-tinyfd::$it"
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 16
}

jar {
    manifest {
        attributes "Main-Class": mainClassName
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
